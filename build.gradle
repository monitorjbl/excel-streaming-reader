plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'com.github.pjfanning'
version = '3.2.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.poi:poi:5.1.0'
    api 'org.apache.poi:poi-ooxml:5.1.0'
    implementation 'com.github.pjfanning:poi-shared-strings:2.2.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'commons-io:commons-io:2.11.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.nanohttpd:nanohttpd:2.3.1'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-to-slf4j:2.14.1'
}

test {
    options {
        systemProperties(System.getProperties())
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'excel-streaming-reader'
                description = 'Streaming Excel reader'
                url = 'https://github.com/pjfanning/excel-streaming-reader'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'monitorjbl'
                        name = 'Taylor Jones'
                        email = 'monitorjbl@gmail.com'
                    }
                    developer {
                        id = 'pjfanning'
                        name = 'PJ Fanning'
                    }
                }
                scm {
                    url = 'https://github.com/pjfanning/excel-streaming-reader'
                    connection = 'scm:git://github.com/pjfanning/excel-streaming-reader.git'
                    developerConnection = 'scm:git://github.com/pjfanning/excel-streaming-reader.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
